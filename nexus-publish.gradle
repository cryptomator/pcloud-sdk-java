apply from: rootProject.file('publishing.gradle')

group = getPublishProperty('groupId')

nexusPublishing {
    repositories {
        sonatype {
            def ossrhUsername, ossrhPassword, sonatypeStagingProfileId = ''

            if (System.getenv().containsKey('CI')) {
                ossrhUsername = System.getenv('OSSRH_USERNAME')
                ossrhPassword = System.getenv('OSSRH_PASSWORD')
                sonatypeStagingProfileId = System.getenv('SONATYPE_STAGING_PROFILE_ID')
            } else {
                File secretPropsFile = project.rootProject.file('local.properties')
                Properties properties = new Properties()
                try(def input = secretPropsFile.newDataInputStream()) {
                    properties.load(input)
                }

                ossrhUsername = properties.getProperty("ossrhUsername")
                ossrhPassword = properties.getProperty("ossrhPassword")
                sonatypeStagingProfileId = properties.getProperty("sonatypeStagingProfileId")
            }

            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = ossrhUsername
            password = ossrhPassword
            stagingProfileId = sonatypeStagingProfileId
        }
    }
}