/*
 * ** Notice of Modification **
 *
 * This file has been altered from its original version by the Cryptomator team.
 * For a detailed history of modifications, please refer to the version control log.
 *
 * The original file can be found at https://github.com/pCloud/pcloud-sdk-java
 *
 * --
 *
 * https://cryptomator.org/
 */

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {

    apply from: rootProject.file('dependencies.gradle')

    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2" }
    }
    dependencies {
        classpath dep.androidPlugin
        classpath dep.kotlinCompiler
    }
}

plugins {
    id("org.jetbrains.dokka") version "1.6.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2" }
    }

    ext.applyForPlugin = { String plugin, Closure<Project> action ->
        applyForPlugins([plugin], action)
    }

    ext.applyForPlugins = { Iterable<String> plugins, Closure<Project> action ->
        for (String plugin : plugins) {
            if (project.plugins.hasPlugin(plugin)) {
                action.call(project)
            } else {
                project.plugins.withId(plugin, {
                    action.call(project)
                })
            }
        }
    }

    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

subprojects {
    applyForPlugins(['com.android.library']) { project ->
        project.with {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion

                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion

                    testInstrumentationRunner dep.testInstrumentationRunner

                }
                buildTypes {
                    release {
						crunchPngs false
						minifyEnabled false
						shrinkResources false

                        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility rootProject.ext.sourceCompatibilityVersion
                    targetCompatibility rootProject.ext.targetCompatibilityVersion
                }
            }
        }
    }

    applyForPlugins(['java']) { project ->
        project.with {
            sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
            targetCompatibility = rootProject.ext.targetCompatibilityVersion
        }
    }
}

tasks.named("dokkaHtmlMultiModule") {
    outputDirectory.set(file("docs"))
}
tasks.withType(KotlinCompile).configureEach {
	compilerOptions {
		jvmTarget.set(JvmTarget.JVM_17)
	}
}
